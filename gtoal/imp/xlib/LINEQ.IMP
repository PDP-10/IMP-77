
%EXTERNALROUTINE SOLVE LN EQ(%LONGREALARRAYNAME A,B,%INTEGER N,%LONG  %C
%REALNAME DET)
%LONGREAL AMAX,CH
%INTEGER  I,J,J MAX,S
-> 3 %IF  N> 0
      PRINTSTRING('
      SOLVE LN EQ DATA FAULT: N=')
      WRITE(N,2); NEWLINE; %STOP
3:-> 1 %IF  N>1
DET=A(1,1)
-> 2 %IF  DET=0
B(1)=B(1)/DET
-> 2
1:DET = 1
%CYCLE  I = 1,1,N-1
A MAX = 0 ; J MAX = 0
%CYCLE  J = I,1,N
-> 4 %IF !A(J,I)!<=AMAX
AMAX=!A(J,I)!; JMAX=J
4: %REPEAT
-> 5 %IF  J MAX = I
DET=-DET
-> 6 %IF  J MAX # 0
DET = 0 ; -> 2
6: %CYCLE  J = I,1,N
CH=A(I,J)
A(I,J)=A(J MAX,J)
A(J MAX,J)=CH
%REPEAT
CH=B(I)
B(I)=B(J MAX)
B(J MAX) = CH
5: CH=A(I,I)
DET = DET*CH
%CYCLE  J = I+1,1,N
A MAX = A(J,I)/CH
%CYCLE  S=I+1 ,1,N
A(J,S)=A(J,S)-A(I,S)*A MAX
%REPEAT
B(J)=B(J)-B(I)*A MAX
%REPEAT
%REPEAT
CH=A(N,N)
DET=DET*CH
-> 2 %IF  DET = 0
B(N)=B(N)/CH
%CYCLE  I=N-1,-1,1
CH=B(I)
%CYCLE  J=I+1,1,N
CH=CH-A(I,J)*B(J)
%REPEAT
B(I)=CH/A(I,I)
%REPEAT
2:
%END


%ENDOFFILE

