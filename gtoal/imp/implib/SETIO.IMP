!SETIO.IMP
! SET STREAMS PROMPTS THE USER WITH 'FILES' AND CAN
! TAKE UP TO 'N' OUTPUT AND 'N' INPUT FILES.
! THESE ARE SPECIFIED BY:-
!	OUTFILE1,OUTFILE2,....OUTFILE'N'=INFILE1,INFILE2,....INFILE'N'
! CORRESPONDING TO STREAMS 1, 2 .... N.  ANY OF THE FILESPECS MAY BE MISSING
! OR LEFT BLANK, IN WHICH CASE THEY DEFAULT TO NUL:.

%CONSTINTEGER MAX STREAMS=3

%EXTERNALROUTINESPEC PROMPT(%STRING(255) STR)
%EXTERNALROUTINESPEC REPORT(%STRING(255) STR)
%EXTERNALROUTINESPEC XDEFINPUT(%INTEGER N,%RECORD(FILESPEC)%NAME FS)
%EXTERNALROUTINESPEC XDEFOUTPUT(%INTEGER N,%RECORD(FILESPEC)%NAME FS)
%EXTERNALROUTINESPEC READFS(%RECORD(FILESPEC)%NAME FC)
%EXTERNALINTEGERFNSPEC SUBEVENT
%EXTERNALINTEGERFNSPEC EVENTINFO

%EXTERNALROUTINE SET STREAMS
%RECORD(FILESPEC)%ARRAY INFS(1:MAX STREAMS), OUTFS(1:MAX STREAMS)
%INTEGER NXTCHAR,N
%ON %EVENT 10 %START
         %UNLESS SUBEVENT=9 %THEN %SIGNAL 10,SUBEVENT,EVENTINFO
         REPORT("Command error: ")
   READSYMBOL(N) %AND PRINTSYMBOL(N) %UNTIL N<' '
   ->RESTART
%FINISH
   %ROUTINE RDFILE(%RECORD(FILESPEC)%NAME FS)
      READFS(FS)
      READSYMBOL(NXTCHAR)
   %END

RESTART:
   %CYCLE
      %CYCLE N=1,1,MAX STREAMS
         INFS(N)=0; OUTFS(N)=0
      %REPEAT
      %CYCLE
         PROMPT("Files:-")
         %IF NEXTSYMBOL>' ' %THEN %EXIT
         SKIPSYMBOL
      %REPEAT
      RDFILE(OUTFS(1))
     %FOR N=2,1,MAX STREAMS %CYCLE
         %EXIT %UNLESS NXTCHAR=','
         RDFILE(OUTFS(N))
      %REPEAT
      %IF NXTCHAR='=' %START
         RDFILE(INFS(1))
         %FOR N=2,1,MAX STREAMS %CYCLE
            %EXIT %UNLESS NXTCHAR=','
            RDFILE(INFS(N))
         %REPEAT
      %FINISH
      %IF NXTCHAR>' ' %THEN %SIGNAL 10,9
      %CYCLE N=1,1,MAX STREAMS
         %IF OUTFS(N)_FILE="" %AND OUTFS(N)_DEV="" %THEN OUTFS(N)_DEV="NUL"
         XDEFOUTPUT(N,OUTFS(N))
         %IF INFS(N)_FILE="" %AND INFS(N)_DEV="" %THEN INFS(N)_DEV="NUL"
         XDEFINPUT(N,INFS(N))
      %REPEAT
      %EXIT
   %REPEAT
%END
%ENDOFFILE
