%EXTERNALSTRING(255)%SPEC ERRMSG
%EXTERNALREALFNSPEC FRACPT(%LONGREAL R)


%ROUTINE FRACTION(%REAL R, %INTEGER PLACES)
   %RETURN %IF PLACES<=0
   PRINTSYMBOL('.')
   %WHILE PLACES>0 %CYCLE
      R = FRACPT(R)*10.0
      PLACES=PLACES-1
      PRINTSYMBOL(INTPT(R)+'0')
   %REPEAT
%END

%EXTERNALROUTINE PRINTFL(%REAL R, %INTEGER P)
   %INTEGER EXP,SIGN
   SIGN=' '
   SIGN='-' %AND R=-R %IF R<0
   PRINTSYMBOL(SIGN)
   %IF R=0 %START
      EXP=-99
   %ELSE
      %IF P<=0 %START
         R=R+0.5
      %ELSE
         R=R+0.5*10\(-P)
      %FINISH
      EXP=0
      EXP=EXP+1 %AND R=R*0.1 %WHILE R >=10.0
      EXP=EXP-1 %AND R=R*10.0 %WHILE R< 1.0
   %FINISH
   PRINTSYMBOL(INT PT(R)+'0')
   FRACTION(R,P)
   PRINTSYMBOL('@'); WRITE(EXP,1)
%END

%EXTERNALROUTINE PRINT(%REAL R, %INTEGER B,A)
   %REAL RM
   %CONSTINTEGER MAX INT= 8_377777777777
   %INTEGER SIGN
   %ROUTINE P(%INTEGER N)
      B = B-1
      P(N//10) %IF N >= 10
      %IF SIGN # 0 %START
         SPACES(B-1)
         PRINTSYMBOL(SIGN);  SIGN = 0
      %FINISH
      PRINTSYMBOL(REM(N,10)+'0')
   %END
   RM=|R|
   PRINTFL(R, B+A) %AND %RETURN %IF RM> MAX INT
   %IF A<=0 %START
      RM=RM+0.5
   %ELSE
      RM=RM+0.5*10\(-A)
   %FINISH
   %IF B<=0 %THEN SIGN=0 %ELSE SIGN=' '
   %IF R<0 %THEN SIGN='-'
   P(INT PT(RM));  !THUS ALLOWING FOR -0.1 ETC.
   FRACTION(RM,A)
%END

%ENDOFFILE
