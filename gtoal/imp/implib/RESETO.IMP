%INCLUDE "IMP:IOLIB.INC"

%CONTROL 16_4000;  !ALLOW RECORDS TO MATCH

%SYSTEMROUTINESPEC FREEVEC(%INTEGER ADR)
%SYSTEMINTEGERFNSPEC GETVEC(%INTEGER SIZE)
%EXTERNALRECORD(SCBNAME)%ARRAYSPEC OUTVEC(-1:MAXCHANS);  !FOR OUTPUT
%EXTERNALRECORD(SCB)%NAMESPEC OUTSCB
%EXTERNALSTRING(255)%SPEC ERRMSG


%SYSTEMPREDICATESPEC FILOP(%RECORD(SCB)%NAME SCB,%INTEGERNAME ERR)
%SYSTEMROUTINESPEC  RELEASE(%INTEGER CHAN)



%EXTERNALROUTINE RESET OUTPUT
   %RECORD(STRSCB)%NAME STRSCB
  %INTEGER ERR,BYTE
  %IF OUTSCB_DEVTYP=UNDEV %OR OUTSCB_DEVTYP=TTYDEV %THEN %RETURN
   %IF OUTSCB_DEVTYP=STRDEV %START;  !A STRING
      STRSCB==RECORD(ADDR(OUTSCB))
      STRSCB_NXTCHR=0
      STRSCB_LENGTH=0
      STRSCB_POINTER=STRSCB_LENPTR
      %RETURN
   %FINISH
  %IF OUTSCB_DEVTYP=TMPDEV %START;  !TMPCOR
      OUTSCB_RINGHEAD_BYTPTR=8_700000000+OUTSCB_RINGHEAD_BUFADR+2;  !RESET POINTER
      OUTSCB_RINGHEAD_BYTCNT=INTEGER(OUTSCB_RINGHEAD_BUFADR+1)*5;  !AND BYTE COUNT
      %RETURN
  %FINISH
   AC(1)=OUTSCB_OBUFOP
   *8_256000000001;  !XCT 1   OUT CHAN,0
   *8_255000000000;   !JFCL
   BYTE=(OUTSCB_RINGHEAD_BYTPTR>>24)&8_77;  !SAVE BYTE SIZE
   RELEASE(OUTSCB_FILOPFN>>18&15)
!WE MUST FREE AND REGET THE BUFFER RING BECAUSE FILOP SETS IT IP
   FREEVEC(OUTSCB_BUFVEC&8_777777);  !FREE OLD BUFFERS
   OUTSCB_RINGHEAD_BUFADR=GETVEC(OUTSCB_BUFVEC>>18)
   OUTSCB_BUFVEC=(OUTSCB_BUFVEC&8_777777000000)!OUTSCB_RINGHEAD_BUFADR
  %UNLESS FILOP(OUTSCB,ERR) %THEN   %C
      ERRMSG="Cannot RESET output stream" %AND %SIGNAL 10,13,OUTSTREAM
   OUTSCB_RINGHEAD_BYTPTR=(OUTSCB_RINGHEAD_BYTPTR&8_777777)!(BYTE<<24);  !RESET BYTE SIZE
%END


%ENDOFFILE



