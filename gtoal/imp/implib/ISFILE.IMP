!+
!  ^THIS LIBRARY CONTAINS THE ROUTINES FOR CHECKING THE EXISTENCE OF
! A FILE.
!-
%INCLUDE "IMP:IOLIB.INC"

%CONSTINTEGER OPEN=8_050, LOOKUP=8_076;  !IOUUO CODES
%CONSTINTEGER SIXDSK=8_446353 000000;  !SIXBIT/DSK/
%CONSTINTEGER SIXTMP=8_645560 000000; !SIXBIT/TMP/
%CONSTINTEGER SIXTTY=8_646471 000000; !SIXBIT/TTY/

%SYSTEMINTEGERFNSPEC GETCHANNEL
%SYSTEMPREDICATESPEC IOUUO(%INTEGER FN,CHAN, %INTEGERNAME ADDR)
%EXTERNALINTEGERFNSPEC STRTOSIX(%STRING(6) STR)
%EXTERNALRECORD(FILESPEC)%FNSPEC STRTOFS(%STRING(255) SPEC)
%SYSTEMROUTINESPEC RELEASE(%INTEGER CHAN)
%SYSTEMINTEGERFNSPEC TMPCOR(%INTEGER TYPE,BLOCK,NAME)
%SYSTEMINTEGERFNSPEC IOWD(%INTEGER LEN, %INTEGERNAME LOC)
%EXTERNALSTRING(6)%FNSPEC JOBFILE(%STRING(3) NAME)
%EXTERNALSTRING(255)%FNSPEC FSTOSTR(%RECORD(FILESPEC)%NAME FS)
%SYSTEMROUTINESPEC FILL PATH BLOCK(%RECORD(PATHBLOCK)%NAME PATH, %RECORD(FILESPEC)%NAME FS, %INTEGER DEVNAM)
%EXTERNALSTRING(255)%SPEC ERRMSG
%OWNRECORD(OPENBLOCK) OPN;    !AN OPEN-BLOCK
%OWNRECORD(LOOKUPBLOCK) LKUP;   !A LOOKUP-ENTER-RENAME BLOCK
%OWNRECORD(PATHBLOCK) PATH;    !A PATH BLOCK

!-
! ^ISFIL DOES THE LOOKUP ON A FILE TO SEE IF IT EXISTS
! RETURNING TRUE OR FALSE AND ALSO THE CHANNEL NUMBER IT USED AS THE
! SECOND PARAMETER. ^THIS CHANNEL MUST BE RELEASED AFTER USE.
!-


%SYSTEMPREDICATE ISFIL(%RECORD(FILESPEC)%NAME FS, %INTEGERNAME CHAN)
!OPEN A DATA CHANNEL
   %RECORD (FILESPEC) WORKFS
   %INTEGER SIXDEV,N
   %INTEGERARRAY TMPBUF(1:4)
   CHAN=0; WORKFS=FS
   OPN_STATUS=8_10;  !IMAGE MODE
   %IF WORKFS_DEV="" %THEN SIXDEV=SIXDSK %ELSE SIXDEV=STRTOSIX(WORKFS_DEV)
   AC(1)=SIXDEV
   *8_047040000064;   !DEVNAM AC1,
   *8_402000000001;   !SETZM AC1
   OPN_DEVNAM=AC(1)
   OPN_BUFHEDS=0;   !NO BUFFERS
   %IF OPN_DEVNAM=SIXTTY %THEN %TRUE; !TTY:
   %IF OPN_DEVNAM=0 %AND SIXDEV=SIXTMP %START;  !TMPCOR
      N=TMPCOR(1,IOWD(4,TMPBUF(1)),STRTOSIX(WORKFS_FILE))
      CHAN=-1
      %IF N#0 %THEN %TRUE
      !ELSE TRY DISK
      OPN_DEVNAM=SIXDSK
      WORKFS_FILE=JOBFILE(FS_FILE); WORKFS_EXT="TMP"
   %FINISH
   CHAN=GETCHANNEL;   !GET NEXT FREE CHANNEL FOR DSK TYPE DEV
   %UNLESS IOUUO(OPEN,CHAN,OPN_STATUS) %THEN  %C
     ERRMSG="Cannot open device for ".FSTOSTR(WORKFS) %AND %SIGNAL 10,6,0;  !FUNNY
!NOW LOOKUP THE FILE
   LKUP_CNT=4
   LKUP_NAM=STRTOSIX(WORKFS_FILE)
   LKUP_EXT=STRTOSIX(WORKFS_EXT)
   LKUP_PRV=0
   LKUP_PPN=ADDR(PATH)
   FILL PATH BLOCK(PATH,WORKFS,OPN_DEVNAM)
   %IF IOUUO(LOOKUP,CHAN,LKUP_CNT) %THEN %TRUE
   RELEASE(CHAN)
   %FALSE
%END


!+
! ^XISFILE TAKES A FILESPEC RECORD AND RETURNS TRUE IF THE FILE EXISTS AND
!FALSE OTHERWISE.
!-

%EXTERNALPREDICATE XISFILE(%RECORD(FILESPEC)%NAME FS)
   %INTEGER CHAN
   %IF ISFIL(FS,CHAN) %START
      RELEASE(CHAN) %UNLESS CHAN<=0
      %TRUE
   %FINISH
   %FALSE
%END


%EXTERNALPREDICATE ISFILE(%STRING(255) SPEC)
%RECORD (FILESPEC) FS
   FS=STRTOFS(SPEC)
   %IF XISFILE(FS) %THEN %TRUE %ELSE %FALSE
%END

%ENDOFFILE

