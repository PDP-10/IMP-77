
%INCLUDE "IMP:IOLIB.INC"

%EXTERNALRECORD(SCB)%NAMESPEC INSCB;            !ADDRESS OF CURRENT SCB START


%EXTERNALSTRING(255)%SPEC ERRMSG

%EXTERNALPREDICATE INPUT PENDING
  %INTEGERARRAY ARGS(0:1)
   %TRUE %IF INSCB_NXTCHR#0
   %IF INSCB_DEVNAM>>18=8_606471 %START;  !FOR PTYS
      %IF INSCB_RINGHEAD_BYTCNT>0 %THEN %TRUE
      AC(1)=INSCB_IBUFOP
      *8_256000 000001;  !XCT [IN CHAN,0]
      %IF INSCB_RINGHEAD_BYTCNT>0 %THEN %TRUE %ELSE %FALSE
   %FINISH
   %IF INSCB_DEVTYP#TTYDEV %THEN %TRUE
   *8_047040 000034;   !GETLIN 1,
   %FALSE %IF AC(1)>>18=0;   !IF DETACHED
   *8_047040000030;    !PJOB  AC1,
   *8_211041000000;    !MOVNI AC1,(AC1)
   *8_047040000061;    !JOBSTS AC1,
   *8_402000000001;    !SETZM AC1
   %FALSE %IF AC(1)&8_100000 000000;   !FALSE IF IT IS AT MONITOR LEVEL
   %IF INSCB_UDX=0 %START;  !SEE IF UDX SET, IF NOT THEN OUR TERMINAL
      *8_051540000000;  !SKPINC   (ANY INPUT)
      *8_634040000001;  !TDZA 1,1
      *8_476000000001;  !SETOM AC1
   %ELSE
      ARGS(0)=1;  !TEST FOR INPUT PENDING
      ARGS(1)=INSCB_UDX
      AC(1)=2<<18!ADDR(ARGS(0))
      *8_047040000116;  !TRMOP 1,
      *8_634040000001;  !TDZA 1,1
      *8_476000000001;  !SETOM 1
   %FINISH
   %IF AC(1)=-1 %THEN %TRUE
   %FALSE
%END


!+
!%EXTERNALROUTINE REPORT(%STRING(255) RSTRING)
! ^OUTPUTS STRING TO THE TERMINAL IRRESPECTIVE OF THE OUTPUT STREAM NUMBER.
!-

%EXTERNALROUTINE REPORT(%STRING(255) RSTRING)
   %INTEGER N
   N=OUTSTREAM;  !SAVE CURRENT OUTPUT STREAM NUMBER
   SELECT OUTPUT(0)
   PRINTSTRING(RSTRING)
   SELECT OUTPUT(N)
%END



%EXTERNALROUTINE PROMPT(%STRING(255) S)
%SYSTEMROUTINESPEC OUTPUT
   %INTEGER N
   %RETURN %IF INSCB_DEVTYP # TTYDEV
   *8_047040 000034;  !GETLIN 1,
   %RETURN %IF AC(1)>>18=0;   !IF DETACHED
   N=OUTSTREAM
   SELECT OUTPUT(0)
   PRINTSTRING(S); OUTPUT
   SELECT OUTPUT(N)
%END
%ENDOFFILE

