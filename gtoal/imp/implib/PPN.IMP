!+
!.AP
! ^THIS LIBRARY CONTAINS ROUTINES TO READ AND WRITE PPNS.
!-


%INCLUDE "IMP:IOLIB.INC"
%EXTERNALROUTINESPEC READ OCTAL(%INTEGERNAME OCT)
%EXTERNALSTRING(12)%FNSPEC OCTTOSTR(%INTEGER N)
%EXTERNALINTEGERFNSPEC PPN

!+
!%EXTERNALROUTINE READPPN(%INTEGERNAME PPN)
! ^READS A PROJECT-PROGRAMMER NUMBER  'PROJ,PROG' INTO PPN
!-

%EXTERNALROUTINE READPPN(%INTEGERNAME APPN)
    %INTEGER PROJ,PROG,N
   %RECORD(PATHBLOCK) PATH
    PROG=0; PROJ=0
   %WHILE NEXTSYMBOL=' ' %THEN SKIPSYMBOL
   %IF NEXTSYMBOL='[' %THEN SKIPSYMBOL
   %IF NEXTSYMBOL='-' %START
      SKIPSYMBOL
      PATH=0
      PATH_FUNCT=-1
      AC(1)=8<<18!ADDR(PATH)
      *8_047040000110;   !PATH AC1,0   SET UP DEFAULT PATH
      *8_255000000000;   !JFCL
      APPN=PATH_PPN
      %RETURN
   %FINISH
    READ OCTAL(PROJ) %UNLESS NEXTSYMBOL=','
    %WHILE NEXTSYMBOL=' ' %THEN SKIPSYMBOL
    %IF NEXTSYMBOL=',' %THEN SKIPSYMBOL;
    READ OCTAL(PROG) %UNLESS NEXTSYMBOL=',' %OR NEXTSYMBOL=']'
   %IF PROJ=0 %OR PROG=0 %START
      N=PPN
      %IF PROJ=0 %THEN PROJ=N>>18
      %IF PROG=0 %THEN PROG=N&8_777777
   %FINISH
    APPN=PROJ<<18!(PROG&8_777777)
%END

!+
!%EXTERNALROUTINE WRITEPPN(%INTEGER PPN)
! ^OUTPUTS THE GIVEN PPN IN THE FORM [ N, N] ON THE CURRENT OUTPUT STREAM.
!-

%EXTERNALROUTINE WRITEPPN(%INTEGER PPN)
    PRINTSYMBOL('[')
    PRINTSTRING(OCTTOSTR(PPN>>18))
    PRINTSYMBOL(',')
    PRINTSTRING(OCTTOSTR(PPN&8_777777))
    PRINTSYMBOL(']')
%END


%ENDOFFILE

