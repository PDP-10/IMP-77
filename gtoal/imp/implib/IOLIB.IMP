
%INCLUDE "IMP:IOLIB.INC"


%OWNINTEGER INPUT, OUTPUT;            !CURRENT CHANNEL NUMBERS

%EXTERNALRECORD(SCBNAME)%ARRAY DASQVEC(1:MAXCHANS); !ADDRESSES OF SCBS FOR EACH DASQ CHANNEL
%EXTERNALRECORD(SCBNAME)%ARRAY INVEC(-1:MAXCHANS);  !ADDRESSES OF SCBS FOR EACH STREAM
%EXTERNALRECORD(SCBNAME)%ARRAY OUTVEC(-1:MAXCHANS);  !FOR OUTPUT
%EXTERNALRECORD(SCB)%NAME INSCB;            !ADDRESS OF CURRENT SCB START
%EXTERNALRECORD(SCB)%NAME OUTSCB


%EXTERNALSTRING(255)%SPEC ERRMSG

%SYSTEMROUTINE SINPUT(%INTEGER N)
INPUT=N
INSCB ==INVEC(N)_NAME
%END


%EXTERNALROUTINE SELINPUT(%INTEGER N)
   %UNLESS 0 <= N <= MAX CHANS %START
        ERRMSG="Selecting illegal input stream"
        %SIGNAL 10,10,N
   %FINISH
   SINPUT(N)
%END



%SYSTEMROUTINE SOUTPUT(%INTEGER N)
OUTPUT=N
OUTSCB ==OUTVEC(N)_NAME
%END


%EXTERNALROUTINE SELOUTPUT(%INTEGER N)
   %UNLESS 0 <= N <= MAX CHANS %START
        ERRMSG="Selecting illegal output stream"
       %SIGNAL 10,11,N
   %FINISH
   SOUTPUT(N)
%END


%EXTERNALROUTINE SPACES(%INTEGER N)
   %RETURN %IF N<=0
   SPACES(N-1)
   SPACE
%END



!+
!%EXTERNALINTEGERFN INSTREAM
! ^RETURNS THE NUMBER OF THE CURRENTLY SELECTED INPUT STREAM.
!-

%EXTERNALINTEGERFN INSTREAM
  %RESULT=INPUT
%END


!+
!%EXTERNALINTEGERFN OUTSTREAM
! ^RETURNS THE NUMBER OF THE CURRENTLY SELECTED OUTPUT STREAM.
!-

%EXTERNALINTEGERFN  OUTSTREAM
  %RESULT=OUTPUT
%END


!+
!%EXTERNALINTEGERFN INDEV
! ^RETURNS THE DEVICE TYPE OF THE CURRENTLY SELECTED INPUT STREAM.
!-

%EXTERNALINTEGERFN INDEV
  %RESULT=INSCB_DEVTYP
%END

!+
!%EXTERNALINTEGERFN OUTDEV
! ^RETURNS THE DEVICE TYPE OF THE CURRENTLY SELECTED OUTPUT STREAM.
!-

%EXTERNALINTEGERFN OUTDEV
  %RESULT=OUTSCB_DEVTYP
%END

%ENDOFFILE

