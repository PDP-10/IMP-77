!+
!  ^THIS LIBRARY CONTAINS THE ROUTINES FOR RENAMING A FILE AND DELETING A FILE.
!-
%INCLUDE "IMP:IOLIB.INC"

%CONSTINTEGER RENAM=8_055;  !IOUUO CODE
%CONSTINTEGER RH=8_777777

%SYSTEMPREDICATESPEC IOUUO(%INTEGER FN,CHAN, %INTEGERNAME ADDR)
%EXTERNALINTEGERFNSPEC STRTOSIX(%STRING(6) STR)
%EXTERNALRECORD(FILESPEC)%FNSPEC STRTOFS(%STRING(255) SPEC)
%SYSTEMROUTINESPEC RELEASE(%INTEGER CHAN)
%SYSTEMINTEGERFNSPEC TMPCOR(%INTEGER TYPE,BLOCK,NAME)
%SYSTEMINTEGERFNSPEC IOWD(%INTEGER LEN, %INTEGERNAME LOC)
%EXTERNALSTRING(255)%FNSPEC FSTOSTR(%RECORD(FILESPEC)%NAME FS)
%SYSTEMROUTINESPEC FILL PATH BLOCK(%RECORD(PATHBLOCK)%NAME PATH, %RECORD(FILESPEC)%NAME FS, %INTEGER DEVNAM)
%EXTERNALSTRING(255)%SPEC ERRMSG
%OWNRECORD(OPENBLOCK) OPN;    !AN OPEN-BLOCK
%OWNRECORD(LOOKUPBLOCK) LKUP;   !A LOOKUP-ENTER-RENAME BLOCK
%OWNRECORD(PATHBLOCK) PATH;    !A PATH BLOCK

%SYSTEMPREDICATESPEC ISFIL(%RECORD(FILESPEC)%NAME FS, %INTEGERNAME CHAN)


%EXTERNALROUTINE XRENAME(%RECORD(FILESPEC)%NAME FS1,FS2)
   %INTEGER CHAN, SIXDEV,DEVNAM,N
   %INTEGERARRAY TMPBUF(1:4)
!LOOKUP THE FILE
   %UNLESS ISFIL(FS1,CHAN) %THEN ->FAILED
!RENAME IT
   %IF FS2_DEV="" %THEN SIXDEV=STRTOSIX("DSK") %ELSE SIXDEV=STRTOSIX(FS2_DEV)
   AC(1)=SIXDEV
   *8_047040000064;   !DEVNAM AC1,
   *8_402000000001;   !SETZM AC1
   DEVNAM=AC(1)
   %IF CHAN=-1 %AND FS2_FILE="" %START; !TMPCOR DELETE
         N=TMPCOR(2,IOWD(4,TMPBUF(1)),STRTOSIX(FS1_FILE))
         %RETURN
   %FINISH
   %IF CHAN<=0 %THEN LKUP_EXT=0 %AND ->FAILED; !OTHERWISE CANNOT RENAME
   LKUP_CNT=4
   LKUP_NAM=STRTOSIX(FS2_FILE)
   LKUP_EXT=STRTOSIX(FS2_EXT)
   LKUP_PRV=FS2_PROT<<27
   LKUP_PPN=ADDR(PATH)
   FILL PATH BLOCK(PATH,FS2,DEVNAM)
   %UNLESS IOUUO(RENAM,CHAN,LKUP_CNT) %THEN ->FAILED
   RELEASE(CHAN)
   %RETURN
FAILED:
   RELEASE(CHAN)
   ERRMSG="Cannot RENAME/DELETE ".FSTOSTR(FS1)
   %SIGNAL 10,16,LKUP_EXT&RH
%END

%EXTERNALROUTINE RENAME (%STRING(255) FILE1,FILE2)
%RECORD(FILESPEC) FS1,FS2
   FS1=STRTOFS(FILE1); FS2=STRTOFS(FILE2)
   XRENAME(FS1,FS2)
%END




%EXTERNALROUTINE XDELETE(%RECORD(FILESPEC)%NAME FS)
   %RECORD(FILESPEC) NUL
   NUL=0
   XRENAME(FS,NUL)
%END

%EXTERNALROUTINE DELETE(%STRING(255) FILE)
%RECORD(FILESPEC) FS
   FS=STRTOFS(FILE)
   XDELETE(FS)
%END


%ENDOFFILE

