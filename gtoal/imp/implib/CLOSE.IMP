%INCLUDE "IMP:IOLIB.INC"
%CONTROL 16_4000;   !ALLOW RECORD EQUATING


%EXTERNALRECORD(SCBNAME)%ARRAYSPEC INVEC(-1:MAXCHANS);  !ADDRESSES OF SCBS FOR EACH CHANNEL
%EXTERNALRECORD(SCBNAME)%ARRAYSPEC OUTVEC(-1:MAXCHANS);  !FOR OUTPUT
%EXTERNALRECORD(SCB)%NAMESPEC INSCB;            !ADDRESS OF CURRENT SCB START
%EXTERNALRECORD(SCB)%NAMESPEC OUTSCB
%EXTERNALRECORD(SCB)%SPEC ITYSCB
%EXTERNALRECORD(SCB)%SPEC OTYSCB
%EXTERNALRECORD(SCB)%SPEC IUNSCB
%EXTERNALRECORD(SCB)%SPEC OUNSCB
%EXTERNALSTRING(255)%SPEC ERRMSG

%CONSTINTEGER OPEN=8_050, LOOKUP=8_076, ENTER=8_077

%EXTERNALROUTINESPEC XDEFOUTPUT(%INTEGER N,%RECORD(FILESPEC)%NAME FS)
%SYSTEMROUTINESPEC FREEVEC(%INTEGER THIS)



%EXTERNALROUTINE CLOSEINPUT
%SYSTEMROUTINESPEC RELEASE(%INTEGER CHAN)
    %INTEGER INSTR
   %RETURN %IF INSCB_DEVTYP=UNDEV
   INSTR=INSTREAM
   %RETURN %IF INSTR=0
   %IF INSCB_DEVTYP#STRDEV %START
      %IF INSCB_DEVTYP=TMPDEV %START
         FREEVEC(INSCB_RINGHEAD_BUFADR)
      %ELSE
          RELEASE(INSCB_FILOPFN>>18&15)
          FREEVEC(INSCB_BUFVEC&8_777777)
      %FINISH
   %FINISH
   FREEVEC(ADDR(INSCB))
!DEFAULT BACK TO UNDEFINED
    INVEC(INSTR)_NAME==IUNSCB
    INSCB==INVEC(INSTR)_NAME
%END


%EXTERNALROUTINE CLOSEOUTPUT
%SYSTEMROUTINESPEC RELEASE(%INTEGER CHAN)
%SYSTEMINTEGERFNSPEC TMPCOR(%INTEGER TYPE,BLOCK,FILENAME)
%SYSTEMINTEGERFNSPEC IOWD(%INTEGER LEN, %INTEGERNAME LOC)
%EXTERNALINTEGERFNSPEC STRTOSIX(%STRING(255) S)
%EXTERNALSTRING(6)%FNSPEC JOBFILE(%STRING(3) NAME)
    %RECORD(FILESPEC) TMPFS
    %RECORD(SCB)%NAME TMPSCB
   %RECORD(STRSCB)%NAME STRSCB
    %INTEGER N,NCHRS,NWDS,OUTST
   %RETURN %IF OUTSCB_DEVTYP=UNDEV
   OUTST=OUTSTREAM
   %RETURN %IF OUTST=0
   %IF OUTSCB_DEVTYP=STRDEV %START
      STRSCB==RECORD(ADDR(OUTVEC(OUTST)_NAME))
      AC(1)=STRSCB_LENPTR
      AC(2)=STRSCB_LENGTH
      *8_137100 000001  ;!DPB 2,1  - LENGTH
      ->RETURN
   %FINISH
    %IF OUTSCB_DEVTYP = TMPDEV %START; !TMPCOR
        NCHRS=TMPCORSIZE*5-OUTSCB_RINGHEAD_BYTCNT;  !NUMBER OF CHARS IN BUFFER
        NWDS=NCHRS//5+1;  !NUMBER OF WORDS IN THE BUFFER
        %CYCLE N=0,1,REM(NCHRS,20);  !REST OF 4 WORDS
            PRINTSYMBOL(0);   !FILL REST OF WORD WITH ZEROS
        %REPEAT
        %IF TMPCOR(3,IOWD(NWDS,INTEGER(OUTSCB_RINGHEAD_BUFADR+2)),STRTOSIX(OUTSCB_TMPNAME)) = 0 %START
!    TMPCOR FAILED, OPEN DSK FILE
            TMPSCB==OUTSCB; OUTVEC(OUTST)_NAME==OUNSCB; OUTSCB==OUTVEC(OUTST)_NAME
            TMPFS_DEV="DSK"; TMPFS_FILE=JOBFILE(TMPSCB_TMPNAME)
            TMPFS_EXT="TMP"; TMPFS_SWITCHES="/MODE:#14"
            XDEFOUTPUT(OUTST,TMPFS)
            SELECT OUTPUT(OUTST)
            %CYCLE N=2,1,NWDS+2;  !NUMBER OF WORDS
                PRINTSYMBOL(INTEGER(TMPSCB_RINGHEAD_BUFADR+N))
            %REPEAT
            FREEVEC(TMPSCB_RINGHEAD_BUFADR)
            FREEVEC(ADDR(TMPSCB))
        %ELSE
           FREEVEC(OUTSCB_RINGHEAD_BUFADR)
           ->RETURN
       %FINISH
    %FINISH
   AC(1)=OUTSCB_OBUFOP;   !OUT CHAN,
   ->RETURN %IF AC(1)=0;  !I.E. A TTY USING TRMOPS
   *8_256000000001;   !XCT 1
   *8_255000000000;   !JFCL
    RELEASE(OUTSCB_FILOPFN>>18&15)
    FREEVEC(OUTSCB_BUFVEC&8_777777)
RETURN:
    FREEVEC(ADDR(OUTSCB))
!DEFAULT BACK TO UNDEFINED
    OUTVEC(OUTST)_NAME==OUNSCB
    OUTSCB==OUTVEC(OUTST)_NAME
%END


%ENDOFFILE
