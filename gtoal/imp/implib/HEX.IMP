!+
!.AP
! ^THIS LIBRARY CONTAINS HEXIDECIMAL READING AND WRITING ROUTINES
!-


%SYSTEMINTEGERFNSPEC STON(%STRING(1)%NAME STR,%ROUTINE READER(%INTEGERNAME N))
%EXTERNALSTRING(255)%SPEC ERRMSG


!+
!%EXTERNALROUTINE READ HEX(%INTEGERNAME N)
! ^READS AN HEX NUMBER FROM THE CURRENT INPUT STREAM INTO N.
!-

%EXTERNALROUTINE READ HEX(%INTEGERNAME N)
    %INTEGER S,SIGN
    S=NEXTSYMBOL
    %WHILE S<=' ' %THEN SKIPSYMBOL %AND S=NEXTSYMBOL
    N=0; SIGN=0
    %IF S='-' %THEN SIGN='-' %AND SKIPSYMBOL %AND S=NEXTSYMBOL
    %UNLESS '0'<=S<='9' %OR 'A'<=S<='F' %START
        ERRMSG="Hex integer not found"
        %SIGNAL 3,1,S
    %FINISH
   %CYCLE
       %IF '0'<=S<='9' %START
         N=N<<4+(S-'0')
         SKIPSYMBOL; S=NEXTSYMBOL
         %CONTINUE
      %FINISH
      %IF 'A'<=S<='F' %START
         N=N<<4+(S-'A'+10)
         SKIP SYMBOL; S=NEXT SYMBOL
         %CONTINUE
      %FINISH
      %EXIT
   %REPEAT
    %IF SIGN#0 %THEN N=-N
%END

!+
!%EXTERNALROUTINE WRITE HEX(%INTEGER N,S)
! ^OUTPUTS A SIGNED HEX INTEGER N WITH S PLACES ON THE CURRENT OUTPUT STREAM.
! ^IF S=0 THEN NO PRECEDING SPACES ARE OUTPUT
!-

%EXTERNALROUTINE WRITE HEX(%INTEGER N,S)
    %INTEGER SIGN,SYM
    %ROUTINESPEC P(%INTEGER N)
    %IF S<=0 %THEN SIGN=S %ELSE SIGN=' '
    S=IMOD(S); S=63 %IF S>63
    %IF N<0 %THEN N=-N %AND SIGN='-'
    P(N)
    %ROUTINE P(%INTEGER N)
        S=S-1
        P(N//16) %IF N>=16
        %IF SIGN > 0 %START
            SPACES(S-1)
            PRINTSYMBOL(SIGN); SIGN=0
        %ELSE %IF SIGN <0 %THEN SPACES(S) %AND SIGN=0
        SYM=REM(N,16)+'0'
        %IF SYM >'9' %THEN SYM=SYM+7
        PRINTSYMBOL(SYM)
    %END
%END


%EXTERNALINTEGERFN STRTOHEX(%STRING(1)%NAME STR)
   %RESULT=STON(STR,READHEX)
%END

%ENDOFFILE
