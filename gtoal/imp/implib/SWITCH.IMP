
%EXTERNALINTEGERFNSPEC STRTOOCT(%STRING(1)%NAME STR)
%EXTERNALINTEGERFNSPEC STRTOINT(%STRING(1)%NAME STR)
%EXTERNALSTRING(255)%SPEC ERRMSG
%CONSTINTEGER MAX SWITCHES=12

%OWNSTRING(11)%ARRAY SW ARRAY(1:MAX SWITCHES)=  %C
   "MODE", "FUNCTION", "ALLOCATE", "BLOCKSIZE", "BUFFERS",   %C
   "DENSITY", "ESTIMATE", "EXTEND", "PARITY", "VERSION", "BYTE", "TRMOP"
%CONSTINTEGER MOD=1, FUNCT=2, ALLOC=3, BLKSIZE=4,  %C
    BUFF=5, DENS=6, EST=7, EXT=8, PAR=9, VERSN=10, BYT=11, TRM=12

%OWNINTEGERARRAYNAME SW ARGS;!** (1:MAX SWITCHES)
%EXTERNALINTEGERARRAY TEMP(0:0);!**TEMP**
%EXTERNALINTEGER MODE;  !VALUES -1 OR 0 - 8_17
%EXTERNALINTEGER FUNCTION; !VALUES -1 OR 1-7
%EXTERNALINTEGER ALLOCATE; !VALUES 0 - 
%EXTERNALINTEGER BLOCKSIZE; !-1 OR 3 - 10000
%EXTERNALINTEGER BUFFNUMS;  !-1 OR <20
%EXTERNALINTEGER DENSITY;  !0 - MAX DENSITIES
%EXTERNALINTEGER ESTIMATE;  !0 -
%EXTERNALINTEGER EXTEND;  !5 - 8_35
%EXTERNALINTEGER PARITY; !0 OR 1
%EXTERNALINTEGER VERSION; !0 -
%EXTERNALINTEGER BYTE; !0 -
%EXTERNALINTEGER TRMOP;
%CONSTINTEGERARRAY SWITCH DEFAULTS(1:MAX SWITCHES)=
      -1,
      -1,
      0,
      -1,
      -1,
      0,
      0,
      5,
      0,
      0,
      0,
      -1

%CONSTINTEGER MAX DENSITIES= 4
%CONSTSHORTINTEGERARRAY DENSITIES(0:MAX DENSITIES) = 0, 200, 556, 800, 1600





%SYSTEMPREDICATE SW MATCH(%STRING(1)%NAME TEST,TARGET)
   %INTEGER N,L,S,T
   L=LENGTH(TEST)
   %FALSE %IF L>LENGTH(TARGET) %OR L=0
   %FOR N=1,1,L %CYCLE
      S=CHARNO(TEST,N)
      S=S-32 %IF 'a'<=S<='z'
      T=CHARNO(TARGET,N)
      T=T-32 %IF 'a'<=S<='z'
      %FALSE %IF S # T
   %REPEAT
   %TRUE
%END


%EXTERNALROUTINE GET SWITCHES(%STRING(1)%NAME SWITCHSTR)
!WHEN A SWITCH IS READ IT IS SET TO THE VALUE OF ITS
!ARGUMENT OR ZERO, OTHERWISE IT IS LEFT AT ITS DEFAULT VALUE
%INTEGER N,LEN
%STRING(39) SW,ARG,SWITCHES
   SWITCHES=SWITCHSTR
   LEN=LENGTH(SWITCHES)
   SW ARGS==TEMP;  !**TEMP**
   %FOR N=0,1,MAX SWITCHES %CYCLE
      SW ARGS(N)=SWITCH DEFAULTS(N)
   %REPEAT

   %ROUTINE SW ERROR(%INTEGER NUMBER)
      ERRMSG="Incorrect argument for switch /".SW ARRAY(NUMBER)
      %SIGNAL 5,9,SWARGS(NUMBER)
   %END

   %RETURN %IF LEN=0
   %IF CHARNO(SWITCHES,1)='/' %THEN SWITCHES=SUBSTRING(SWITCHES,2,LEN)
NEXT:
   ->CHECK %IF SWITCHES=""
   %UNLESS SWITCHES->SW.("/").SWITCHES %START
      SW=SWITCHES; SWITCHES="";   !LAST SWITCH
   %FINISH
   %UNLESS SW->SW.(":").ARG %THEN ARG=""
   %FOR N=MOD,1,MAX SWITCHES %CYCLE
      %IF SWMATCH(SW,SWARRAY(N)) %START
         %IF ARG#"" %START
            %IF CHARNO(ARG,1)='#' %START
               ARG=SUBSTRING(ARG,2,LENGTH(ARG))
               SWARGS(N)=STRTOOCT(ARG)
            %ELSE SWARGS(N)=STRTOINT(ARG)
         %ELSE SWARGS(N)=0
         -> NEXT
      %FINISH
   %REPEAT
!HERE FOR NO MATCH
   ERRMSG="Unknown switch /".SW
   %SIGNAL 5,10

CHECK:   ;!HERE TO CHECK SWITCH VALUES
   %IF EXTEND#0 %START
      %UNLESS 5<=EXTEND<=8_35 %THEN SW ERROR(EXT)
   %ELSE EXTEND=8_35

   %IF VERSION#0 %START
      EXTEND=6 %IF EXTEND<6
   %FINISH

   %IF ESTIMATE #0 %START
      EXTEND=8 %IF EXTEND<8
   %FINISH

   %IF ALLOCATE# 0 %START
      EXTEND=9 %IF EXTEND<9
   %FINISH

   %UNLESS -1<=BUFFNUMS<=20 %THEN SW ERROR(BUFF)

   %UNLESS BLOCKSIZE=-1 %OR 3<=BLOCKSIZE<=10000 %THEN SW ERROR(BLKSIZE)

   %IF FUNCTION#-1 %START
      %UNLESS 1<=FUNCTION<=7 %THEN SWERROR(FUNCT)
   %FINISH

   %IF 0#PARITY#1 %THEN SW ERROR(PAR)

   %IF DENSITY#0 %START
      %FOR N=1,1,MAX DENSITIES %CYCLE
         %IF DENSITY=DENSITIES(N) %THEN DENSITY=N %AND ->DENSITY OK
      %REPEAT
      SW ERROR(DENS)
   %FINISH
DENSITY OK:
%END

%ENDOFFILE
!NOW A TEST SECTION

%EXTERNALROUTINESPEC READFS(%RECORD(FILESPEC)%NAME FS)
%INTEGER GOT,I
%RECORD (FILESPEC) FS
%CYCLE
PRINTSTRING("FILE: "); READFS(FS)
GET SWITCHES(FS_SWITCHES)
%FOR I=1,1,MAX SWITCHES %CYCLE
   WRITE(SWARGS(I),3)
%REPEAT
NEWLINES(2)
%REPEAT
%ENDOFPROGRAM
