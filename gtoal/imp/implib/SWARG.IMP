
%EXTERNALINTEGERFNSPEC STRTOOCT(%STRING(1)%NAME STR)
%EXTERNALINTEGERFNSPEC STRTOINT(%STRING(1)%NAME STR)
%EXTERNALSTRING(255)%SPEC ERRMSG


%SYSTEMPREDICATESPEC SW MATCH(%STRING(11)%NAME TEST,TARGET)


%EXTERNALPREDICATE SWITCH ARG(%STRING(1)%NAME SOURCE,%STRING(11) TARGET, %NAME ARGUMENT)
!RETURNS THE ARGUMENT OF A SWITCH IF ONE IS GIVEN
!AND %FALSE IF NO SWITCH FOUND TO MATCH THE TARGET
%INTEGERARRAY ARG FRIG(0:0);  !SO ARGUMENT=ARG FRIG(-1)
%STRING(255) SWITCHES,SW
%STRING(25) SW1,ARG
%INTEGER TYPE,N,CH,LEN
   TYPE=ARG FRIG(-1)>>23&8_17
   %IF TYPE>4 %START
      ERRMSG="Illegal %name type parameter"
      %SIGNAL 5,6,type
   %FINISH
   SW=""
   LEN=LENGTH(SOURCE)
   INTEGER(ADDR(ARGUMENT))=0; !ZERO RESULT
   %FALSE %IF LEN=0
   SWITCHES=SOURCE
   SOURCE=""
!REMOVE FIRST SLASH
   %IF CHARNO(SWITCHES,1)='/' %START
      SWITCHES=SUBSTRING(SWITCHES,2,LENGTH(SWITCHES))
   %FINISH
   %CYCLE
      %UNLESS SWITCHES->SW.("/").SWITCHES %START; !GET ONE SWITCH
         SW=SWITCHES; SWITCHES="";  !LAST ONE
     %FINISH
      %FALSE %IF SW=""; !NO MORE FOUND
      %UNLESS SW->SW1.(":").ARG %THEN SW1=SW %AND ARG="";  !GET ANY ARG
      %IF SWMATCH(SW1,TARGET) %START
         %IF ARG#"" %START
            %IF TYPE=4 %START;  !STRING TYPE
               STRING(ADDR(ARGUMENT))=ARG
            %ELSE;   !INTEGER TYPE
               %IF CHARNO(ARG,1)='#' %START;   !OCTAL NUMBER
                  ARG=SUBSTRING(ARG,2,LENGTH(ARG))
                  INTEGER(ADDR(ARGUMENT))=STRTOOCT(ARG)
               %ELSE INTEGER(ADDR(ARGUMENT))=STRTOINT(ARG)
            %FINISH
         %FINISH
         SOURCE=SOURCE."/".SWITCHES %UNLESS SWITCHES="";  !OMIT THE MATCHED SWITCH
         %TRUE
      %FINISH
      SOURCE=SOURCE."/".SW;  !REBUILD SOURCE - OMIT THE MATCHED SWITCH
   %REPEAT
%END
%ENDOFFILE
!NOW A TEST SECTION

%EXTERNALROUTINESPEC READFS(%RECORD(FILESPEC)%NAME FS)
%INTEGER GOT,I
%RECORD (FILESPEC) FS
%CYCLE
PRINTSTRING("FILE: "); READFS(FS)
GET SWITCHES(FS_SWITCHES)
%FOR I=1,1,MAX SWITCHES %CYCLE
   WRITE(SWARGS(I),3)
%REPEAT
NEWLINES(2)
%REPEAT
%ENDOFPROGRAM
