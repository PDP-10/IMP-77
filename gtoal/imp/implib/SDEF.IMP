%INCLUDE "IMP:IOLIB.INC"
%CONTROL 16_4000;   !ALLOW RECORD EQUATING


%EXTERNALRECORD(SCBNAME)%ARRAYSPEC INVEC(-1:MAXCHANS);  !ADDRESSES OF SCBS FOR EACH CHANNEL
%EXTERNALRECORD(SCBNAME)%ARRAYSPEC OUTVEC(-1:MAXCHANS);  !FOR OUTPUT
%EXTERNALRECORD(SCB)%NAMESPEC INSCB;            !ADDRESS OF CURRENT SCB START
%EXTERNALRECORD(SCB)%NAMESPEC OUTSCB
%EXTERNALINTEGERSPEC RDST;   !STRING READ ROUTINE -FRIG
%EXTERNALINTEGERSPEC WTST;   !STRING WRITE ROUTINE -FRIG

%SYSTEMROUTINESPEC GETSCB(%RECORD(SCBNAME)%NAME R,%INTEGER SIZE)
%SYSTEMROUTINESPEC SCHECK(%INTEGER N,CHNTYP, %RECORD(SCBNAME)%NAME IOSCB)


%SYSTEMROUTINE SD INPUT(%INTEGER N,%STRING(1)%NAME STR)
   %RECORD(STRSCB)%NAME STRSCB
   GETSCB(INVEC(N),STRSCBSIZE)
   STRSCB==RECORD(ADDR(INVEC(N)_NAME))
   STRSCB_DEVTYP=STRDEV
   STRSCB_OPER==RDST;   !READ ROUTINE
   STRSCB_NXTCHR=0
   STRSCB_LENPTR=8_331100<<18!ADDR(STR)&8_777777
   STRSCB_POINTER=STRSCB_LENPTR
   AC(2)=STRSCB_LENPTR
   *8_135040 000002;  !LDB 1,2
   STRSCB_LENGTH=AC(1)
   %IF INSTREAM=N %THEN INSCB==INVEC(N)_NAME
%END

%EXTERNALROUTINE SDEF INPUT(%INTEGER N,%STRING(1)%NAME STR)
   SCHECK(N,STREAM,INVEC(N))
   SDINPUT(N,STR)
%END




%SYSTEMROUTINE SD OUTPUT(%INTEGER N,%STRING(1)%NAME STR)
   %RECORD(STRSCB)%NAME STRSCB
   GETSCB(OUTVEC(N),STRSCBSIZE)
   STRSCB==RECORD(ADDR(OUTVEC(N)_NAME))
   STRSCB_DEVTYP=STRDEV
   STRSCB_NXTCHR=0
  STRSCB_OPER==WTST;   !WRITE ROUTINE
   STRSCB_LENPTR=8_331100<<18!ADDR(STR)&8_777777
   STRSCB_POINTER=STRSCB_LENPTR
   STRSCB_LENGTH=0
   %IF OUTSTREAM=N %THEN OUTSCB==OUTVEC(N)_NAME
%END

%EXTERNALROUTINE SDEF OUTPUT(%INTEGER N,%STRING(1)%NAME STR)
   SCHECK(N,STREAM,OUTVEC(N))
   SDOUTPUT(N,STR)
%END


%ENDOFFILE
