MODULE TELL(STACK,RESERVE(4,5,6),VERSION=4B(321))=
!SEE TELPRM.BLI FOR COMPILATION OPTIONS.
!
!REVISION HISTORY:
!EDIT-1  K.F. UNIV OF EDIN.
!	1) EXTENDED LIST OF NUMBERS NOT TO BE TOLD - NTELL
!	2) INCLUDE A ROUTINE CALLED 'YES CHECK' (IN TELSUB.BLI)
!	 WHICH ONLY ALLOWS A RESPONSE OF YES OR NO. THIS IS USED BY
!	TELL: IN TELCOM MODULE TO MAKE SURE OF A REPLY FROM
!	"??SEND SAME MESSAGE TO OTHER USERS."
!	3) only allow ^Z to terminate a message(READMESSAGE IN TELCOM)
!	4) define ATFILESIZE in TELPRM to be maximum size in blocks
!	   that an indirect file may be. ( MM @ ERCC)
!
!
!EDIT-2 K.F. UNIV OF EDIN
!	INCLUDE A /BATCH SWITCH TO INDICATE THAT THERE IS NO INTERACTIVE
!	USER PRESENT AND NOT TO EXPECT A REPLY TO "WHO?" AND
!	"DO YOU WANT TO TELL ANYBODY ELSE"
!	(TELPRM, TELSUB(SWITCHRTN), TELCOM(TELLCOMMAND), TELACT)
!
!VERSION 1	APRIL 73 SJF
!VERSION 2	JUNE 73 SJF 
!VERSION 3(76)	APRIL 75 PLD
!VERSION 3A(???) PLD
!VERSION 4(200)	THE FIRST TELL TO BE WRITTEN IN BLISS.
!		EDIT NUMBER ARBITRARILY CALLED 200.
!EDIT 201	USE QUICKSORT INSTEAD OF BUBBLE SORT TO SORT NAME TABLE
!EDIT 202	MAKE THE DEFAULT DISPOSITION FOR MESSAGES ON HOLD TO BE HOLD
!		MESSAGES ON HOLD CAN ONLY BE DELETED IF DELETE IS EXPLICITLY TYPED.
!EDIT 203	IF NO COMMAND IS FOUND IN BUFFER WHEN FIRST DO A RESCAN, PRETEND
!		WE SAW A MESSAGE COMMAND.  THIS IS SO LOGIN CAN IMPLEMENT A
!		/MAIL:PRINT WHICH WORKS BY DOING A RUN UUO TO TELL.
!EDIT 204	ADD A CSL HACK TO CHECK TO SEE IF A RECEIVER IS ON THE SYSTEM
!		WHEN A TELL IS DONE TO HIM, AND IF SO TO TYPE A MESSAGE ON HIS
!		TERMINAL.  WON'T TYPE ON A PTY OR IF SET TTY GAG OR NO SEND SET.
!EDIT 205	MAKE SAVE IN RESPONSE TO A DISPOSE OR ACTION QUERY FROM MESSAGE
!		TURN OFF THE HOLD BIT OF A MESSAGE.
!EDIT 206	GENERAL CODE CLEAN UP IN TELL.  MAKE OPENMAILFILE DO THE MASSAGING
!		OF INPUT FILE RATHER THAN IN APPENDMESSAGE.  MOVE THE CALL TO
!		TELLHIM TO SENDMAIL.  ALLOW A USER TO FORWARD MAIL TO HIMSELF
!		BY CALLING SENDMAIL IF TELL WAS CALLED FROM MESSAGE (KLUDGE!)
!EDIT 207	MAKE /HOLD PRINT ONLY MESSAGES ON HOLD; IF OTHER MESSAGES ARE
!		PRESENT TYPE THE MESSAGE [YOU HAVE MESSAGES NOT ON HOLD].  ALSO
!		SET THE ANYONHOLD BIT FOR LOGIN IF THERE ARE ANY MESSAGES ON
!		HOLD.
!EDIT 210	IF IN A BATCH JOB, MAKE THE DEFAULTS FOR MESSAGE BE
!		/QUERY:NEVER/DISPOSE:SAVE.  THIS OVERRIDES SWITCH.INI BUT
!		NOT A COMMAND TYPED ON THE MESSAGE LINE.
!EDIT 211	FIX A BUG IN TELL SUCH THAT IF IT ASKS SEND SAME MESSAGE TO
!		OTHER USERS IT TRIES TO SEND THE MESSAGE TO THE SAME USERS
!		IT JUST DID SEND TO.
!EDIT 212	FIX A COUPLE OF BUGS RELATED TO SCANNING PAST THE END OF THE
!		NAME TABLE.
!EDIT 213	SWITCH IN A TELL LINE ZAPS THE TELLIST.  FIX:YES
!EDIT 214	GROUPNAME FLAG DOESN'T GET CLEARED IN FINDNAM.  FIX: CLEAR IT
!EDIT 215	PROGRAMMER STUPIDITY: STRNAME MAPS ON A VARIABLE THAT CONTAINS
!		THE ADDRESS OF A NAME BLOCK, NOT ON THE NAME BLOCK ITSELF.
!		FIX: YES
!EDIT 216	TELL SOMETIMES CAME BACK WITH AN EXTRA PROMPT
!EDIT 217	IMPROVE FORMAT OF TIME IN MESSAGE LINE (ASCII)
!EDIT 220	PPN'S PRECEDED BY A - DIDN'T GET REMOVED FROM THE LIST
!EDIT 221	WHEN PASSING OPTIONS TO NAMESEARCH (RECURSIVELY), THE
!		ARGUMENT MUST BE DOTTED!
!EDIT 222	SOMETIMES WHEN BUFFERED I/O WAS USED, THE COUNT WAS NOT BEING DECREMENTED
!		IN THE BUFFER HEADER PROPERLY, CAUSING DIVERSE ERRORS.
!		(SUCH AS MESSAGE FILE CORRUPTION)
!EDIT 223	PROVIDE ALIAS COMMAND (PRIVILEGED) FOR DEBUGGING
!EDIT 224	IF GAG IS SET, STILL INFORM USER IF AT MONITOR LEVEL
!EDIT 225	SUPPRESS SPURIOUS PROMPT IF READING A SEVERAL LINE MESSAGE
!		FROM A FILE.
!EDIT 226	CORRECT MISSPELLING OF SEPTEMBER. (DEFENSIVE) IGNORE A 
!		ZERO LENGTH MESSAGE. INPUT ERROR CONTINUES, SO USE ERRORC, NOT ERROR.
!EDIT 227	REPLYING TO A MESSAGE OVERWROTE SAVED MESSAGE WITH REPLY
!EDIT 230	PUT IN SEVERAL SUGGESTIONS FROM SJF (INCLUDING BUG FIXES...)
!EDIT 231	PPNSEARCH RETURNED 1 WHEN IT SHOULD RETURN 0, AND VICE VERSA
!		ALSO, IGNORE NULL COMMANDS TO TELL.
!EDIT 232	COPYRESTOFLINE MADE NO CHECKS TO MAKE SURE IT'S LINE BUFFER
!		DID NOT OVERFLOW, CAUSING ILL MEM REFS.
!EDIT 233	TELL IS A SECURITY RISK! MUST CHKACC INDIRECT FILES FOR READ
!		AND OUTPUT COPY FILES FOR WRITE
!EDIT 234	PROVIDE A PARAMETER WHICH IS THE DEFAULT MAIL FILE PROTECTION.
!EDIT 235	IF INDPPN, MAIL FILE NAME IS THE PPN (LIKE UFD)
!EDIT 236	PHYSICAL ONLY LOOKUP OF ACCT.SYS
!EDIT 237	REMOVE ALL REFERENCES TO SYSTEM GROUPS, SINCE THEY WILL NOT
!		BE IMPLEMENTED.
!EDIT 240	GETFILE MUST RESERVE 3 WORDS FOR A BUFFER HEADER, NOT 1!
!EDIT 241	REBUILD NAME TABLE ANY TIME ACCT.SYS CHANGES.
!EDIT 242	IMPROVE TELI/O MESSAGE. CREATE EXIT OPTION FOR DISPOSE:
!EDIT 243	IF A GROUP NAME IS AMBIGUOUS, TELL FORGETS THAT IT IS
!		A GROUP NAME, AND TELLS INTO A FILE 000000.MAI
!EDIT 244	IF COPY FILE ALREADY EXISTS, APPEND TO IT. ALSO, DEFPROT
!		COMES LEFT JUSTIFIED!
!EDIT 245	ADD WHO COMMAND
!EDIT 246	'TELL *,PROG' IS NOT PRIVILEGED, IF INDPPN.
!EDIT 247	THE MONITOR SOMETIMES WILL NOT ACCEPT A BUFFER IF THE BYTE
!		POINTER IS NOT UPDATED, CAUSING MAIL FILE DELETION!
!EDIT 250	MONITOR KILLED TELL IF TRIED TO COPY TO LPT:
!EDIT 251	IMPLEMENT NEVER-TELL LIST.
!EDIT 252	ONLY ASSUME THIS IS A MESSAGE COMMAND IF ENTERED AT CCL ENTRY
!EDIT 253	IF NOT LOGGED IN, MUST TYPE OWN . WHEN EXIT.
!EDIT 254	SUPPORT CSL'S NEW ACCT.SYS (UNDER CSLHACK CONDITIONAL)
!EDIT 255	INFORM MESSAGE READER IF CAN'T GET MESSAGE FILE.
!EDIT 256	WHEN TYPING ON OTHER TERMINAL, CHECK ONLY PROGRAMMER NUMBER
!		IF NOT INDPPN. REMOVE EDIT 204 FROM CSLHACK CONDITIONAL.
!EDIT 257	DEFAULT EXTENSION FOR A COPY FILE (IF NONE SPECIFIED) IS .MAI
!EDIT 260	ALLOW SWITCHES AFTER NAME LIST, BUT BEFORE START OF MESSAGE
!		ON THE SAME LINE. FIX AN ADDRESS CHECK.
!EDIT 261	LOOKUP SWITCH.INI IN THE UFD, NOT DEFAULT PATH.
!EDIT 262	IF NO PPN IS SPECIFIED (FOR ANY FILE) THE DEFAULT PATH
!		SHOULD BE USED.  INSTEAD, THE UFD IS USED.
!EDIT 263	'H' IS AN UNAMBIGUOUS ABBREVIATION FOR HELP. ADD A SPACE
!		AFTER THE COMMA IN A DATE IN THE MESSAGE HEADER
!EDIT 264	SEPARATE MESSAGES BY A BLANK LINE, FOR READABILITY
!EDIT 265	PUT START ADDRESS IN A MACRO MODULE, SO THAT PORTALS CAN BE
!		DONE.
!EDIT 266	CLEAR TO END-OF-LINE WHEN READING INDIRECT FILE SPEC
!EDIT 267	CLEAR MORESENDERS BIT WHEN FIXING UP FIRST BLOCK
!EDIT 270	TYPE 'KJOB' IF NOT LOGGED IN. USE LOGOUT UUO RATHER THAN
!		EXIT UUO.
!EDIT 271	ALLOW /DIS:COPY:FILE-SPEC ON COMMAND LINE AND IN SWITCH.INI.
!		FIX A BUG IN READSPEC ROUTINE
!EDIT 272	FIX BUG RELATED TO SFD'S AND COPY COMMAND
!EDIT 273	UNDERSTAND SWITCH.INI WITH SOS LINE NUMBERS
!EDIT 274	WRONG CHANNEL USED IN SETUP IN TELMES
!EDIT 275	ALLOW CONTINUES AFTER A ^C (WHEN DETECTED IN NEXTCHAR)
!EDIT 276	FIX /DIS:COPY:FILE WHEN NOT QUERYING
!EDIT 277	MAKE QUERY:NONE BE THE SAME AS QUERY:NEVER
!EDIT 300	USE PHYSICAL ONLY WHEN FINDING MAIL FILE
!EDIT 301	IF MESSAGE IS GTR THAN MAXLINES (COMPILE PARAMETER)
!		LINES LONG, ASK USER IF HE WANTS TO SEE IT, BEFORE TYPING.
!EDIT 302	ROUTINE YES WILL NOW ACCEPT ONLY A YES, A NO, OR AN EOL.
!EDIT 303	RECLAIM DISK BUFFERS BETWEEN MULTI-USER TELLS
!EDIT 304	SAY '%YOU CAN'T TELL [P,PN]' WHEN REMOVING A PPN THAT IS
!		ON THE NEVTELL LIST
!EDIT 305	MAKE '/QUERY:N' BE UNAMBIGUOUS FOR '/QUERY:NEVER' OR
!		'/QUERY:NONE'
!EDIT 306	'ANYONHOLD' FLAG GOT SET IF ANY MESSAGES WERE ENCOUNTERED
!		THAT WERE ON HOLD--NOT JUST IF ANY MESSAGES WERE KEPT
!		THAT WERE ON HOLD.
!EDIT 307	DOCUMENT ALL CHOICES FOR QUERY:BEFORE--NOT JUST THE 'SENSIBLE'
!		ONES.
!EDIT 310	WHEN CREATING A MAIL FILE, PUT THE USER'S NAME IN THE LAST
!		SLOT FOR SENDER'S NAMES.
!EDIT 311	CREATE /UNREAD SWITCH; WITH THIS, ONLY THOSE MESSAGES THAT
!		YOU HAVE NOT READ WILL BE SHOWN TO YOU
!EDIT 312	5 BUGS IN DATE/TIME CODE: 1) NO <CRLF> AFTER TELITS.
!		2) NO <CRLF> AFTER TELIMS 3) YEAR 76 DOES NOT MEAN 1976
!		4) CANNOT INCLUDE A TIME IF A YEAR WAS SPECIFIED
!		5) TELL (9 17 1976) TELLS AFTER OCT 17, NOT SEPT 17
!		(PATCHES COURTESY OF AFAL)
!EDIT 313	MAIL COMMAND SAME AS TELL COMMAND.
!EDIT 314	ALLOW WILD PROJECT NUMBERS IN NTELL LIST, TOO. (PATCH COURTESY
!		OF AFAL)
!EDIT 315	ONLY CHECK <RH> WHEN LOOKING FOR A NON-0 MESSAGE LENGTH.
!EDIT	316	CHANGE TELL MAIDEV TO BE SSL (SYSTEM SEARCH LIST) WHICH WORKS
!		FINE ON BOTH DEC10S
!EDIT	317	ALLOW [1,2] TO TELL INDIRECT FILES OF ANY LENGTH
!EDIT	320	ALLOW RESOURCE MANAGEMENT SUPERVISORS TO TELL TO ANYONE
!		IN THEIR DEPARTMENT.
!EDIT	321	ALLOW PPNS TO BE SPECIFIED WITHOUT ",", IE 1035006742
!		IS 1035,6742. MODS TO TELCOM.
BEGIN

REQUIRE DSK:TELPRM.BLI;
EXTERNAL    BUILDNAMETABLE,
	    MACINI,
	    DISPOPTION,
	    TELLCOMMAND,
	    CLREOL,         !ROUTINE TO CLEAR INPUT BUFFER TO EOL
	    RESCAN,
	    MTCHNM,
	    FILESPEC,	    !PLACE TO BUILD FILE SPECIFICATIONS
	    OPENSPEC,	    !OPENS FILE SPECIFICATION
	    DSKBUFH,	    !BUFFER HEADER FOR CHANNEL DSK
	    ZEROSPEC,	    !ZEROES FILESPEC
	    GROUPLIST,	    !LIST OF NAMES IN USER-DEFINED GROUPS
	    CHARBSU,	    !RETURNS CHARACTER IN BSU FORMAT
	    SWITCHRTN,	    !PROCESSES SWITCHES
	    MESSAGECOMMAND, !PRINTS MESSAGES, OTHER NEAT THINGS
	    WHOCOMMAND,	    !PERFORMS WHO COMMAND
	    SIXREAD,
	    NAMEBUILT,	    !NON-ZERO IF NAME TABLE BUILT (CONTAINS ACCT.SYS UNIV CREATION DATE/TIME)
	    NAMESIZE,	    !CONTAINS SIZE OF ACCT.SYS WHEN NAME TABLE WAS BUILT
	    NAMEPOS,	    !CONTAINS DISK POSITION OF ACCT.SYS WHEN NAME TABLE WAS BUILT
	    LOCK,	    !INTERLOCK TO PREVENT TWO OR MORE JOBS FROM BUILDING TABLE
	    TELLHELP,	    !HELP MESSAGE
	    STOP,	    !ROUTINE TO EXIT
	    ALIASCOMMAND,   !ROUTINE TO SET ALIAS
	    FLAGWORD;


!TABLE OF COMMANDS TO TELL (ORDER MUST AGREE WITH CASE STATEMENT IN MAIN PROGRAM).
BIND COMTAB=PLIT(
SIXBIT 'START', !0 -- AFTER GET
SIXBIT 'RUN',   !1 -- THIS ALSO RECOGNIZES 'R'
SIXBIT 'TELL',  !2 -- SOMEBODY SOMETHING
SIXBIT 'MESSAG',!3 -- WHATEVER SOMEBODY TOLD US
SIXBIT 'HELP',	!4 -- PRINT HELP
SIXBIT 'WHO',	!5 -- THE WHO COMMAND
SIXBIT 'ALIAS',	!6 -- THE ALIAS COMMAND
SIXBIT 'MAIL');	!7 -- THE MAIL COMMAND


MAP STRFILE FILESPEC;
MAP STRCOREBLK GROUPLIST;


OWN	SAVEFLAGS,		!SAVE FLAGS FROM SWITCH.INI
	RUNFLAG,		!1 IF ENTERED BY RUN COMMAND
	SIMULATEMESSAGE,	!1 IF ENTERED BY RUN UUO (FROM LOGIN HOPEFULLY)
	SAVEJOBFF;		!FIRST FREE LOCATION IN LOW SEGMENT
!ROUTINE TO CHECK TO SEE IF NAME TABLE IS BUILT; IF NOT TO GO BUILD IT.
!THERE IS A "LOCK", CONSISTING OF A VARIABLE INITIALIZED TO -1 IN THE HIGH
!SEG.  IN ORDER TO PREVENT TWO COPIES OF TELL FROM TRYING TO BUILD THE HIGH
!SEGMENT AT THE SAME TIME, THE LOCK IS INCREMENTED AND TESTED FOR NON-ZERO.
!FOR EXACTLY ONE JOB WILL THE LOCK BE -1; THIS JOB WILL THEN BUILD THE SEGMENT.
!
!FOR THIS CODE TO WORK CORRECTLY, BLISS MUST BE SMART ENOUGH TO
!GENERATE AN AOSX INSTRUCTION WHEN TESTING LOCK.  CURRENTLY IT DOES...
!
ROUTINE CHECKBUILD=
BEGIN
MACRO SETUWP(CODE)=
BEGIN REGISTER S;
S_CODE; CALLI(S,#36); JFCL(0,0); !IF IT FAILS, WILL GET ERROR SOON ENOUGH...
END$;

LABEL L;
OWN ACCTDATE;			!UNIVERSAL DATE/TIME OF ACCT.SYS
LOCAL ACCTLBLOCK[30];		!EXTENDED LOOKUP BLOCK FOR ACCT.SYS

FILESPEC[FILESTATUS]_#400000000010;	!PHYSICAL ONLY
FILESPEC[FILEDEVICE]_SIXBIT 'SYS';	!OPEN OF SYS:
ACCTLBLOCK[1]_FILESPEC[FILEBUFS]_0;		!NO BUFFERS
ACCTLBLOCK[0]_#35;		!ALL RIB WORDS THROUGH .RBTIM
ACCTLBLOCK[2]_SIXBIT 'ACCT';
ACCTLBLOCK[3]_SIXBIT 'SYS';
IFSKIP OPEN(DSK,FILESPEC[FILESTATUS]<ADDR>)
    THEN IFSKIP LOOKUP(DSK,ACCTLBLOCK<ADDR>)
	THEN ACCTDATE_.ACCTLBLOCK[29]
	ELSE STOP()
    ELSE STOP();
!** ENHANCE CHECK TO SEE IF FILE IS SAME SIZE AND AT SAME DISK POSITION ** 
!IF .ACCTDATE EQL .NAMEBUILT THEN RETURN;
IF .ACCTDATE EQL .NAMEBUILT THEN IF .ACCTLBLOCK[#5] EQL .NAMESIZE THEN IF .ACCTLBLOCK[#12]
    EQL .NAMEPOS THEN RETURN;
SETUWP(0);                      !WRITE ENABLE THE HIGH SEGMENT
L: IF (LOCK_.LOCK+1) EQL 0      !CHECK THE INTERLOCK
    THEN BUILDNAMETABLE()       !NOBODY ELSE IS BUILDING, GO DO IT OURSELVES
    ELSE
	BEGIN                   !WAIT 45 SECONDS, IF NOT BUILT BY THEN
	DECR J FROM 45 TO 1 DO
	    (SLEEP(1); IF .NAMEBUILT EQL .ACCTDATE THEN LEAVE L);
	BUILDNAMETABLE();       !GO AHEAD AND BUILD IT ANYWAY (ASSUME SOMEBODY DIED)
	END;
NAMEBUILT_.ACCTDATE;            !SAY WE BUILT THE NAME TABLE
NAMESIZE _ .ACCTLBLOCK[#5];	!REMEMBER SIZE OF ACCT.SYS
NAMEPOS _ .ACCTLBLOCK[#12];	!AND ITS DISK POSITION - THIS MAY HELP USE DETECT WHEN IT HAS BEEN UPDATED
LOCK_-1;			!RESET LOCK
SETUWP(1);                      !WRITE LOCK THE HIGH SEGMENT
END;


MACRO	    RESCANLINE=TTCALL(#10,1)$;      !SKIPS IF NO COMMAND AVAILABLE
!
!ROUTINE TO DO A LOT OF GETTABS TO GET VARIOUS JOB-RELATED INFORMATION.
!ANY SUNDRY INITIALIZATION SHOULD BE PUT INTO THIS ROUTINE.
!
GLOBAL	DAY,		!DAY OF THE MONTH
	MONTH,		!MONTH-1 OF THE YEAR
	YEAR,		!WHAT YEAR THIS IS (4 DIGIT)
	TIME,		!MINUTES SINCE MID NIGHT
	NOW,		!NOW IN UNIVERSAL FORMAT
	WEEKDAY,	!DAY OF WEEK (SUNDAY=0)
	LOGGEDIN,	!1 IF JOB IS LOGGED IN
	DEFPROT,	!DEFAULT FILE PROTECTION
	MYNAME[2],	!MY NAME IN SIXBIT
	MYJOB,		!JOB NUMBER
	MYPPN[3];	!MY PPN; THIS MUST BE A THREE WORD BLOCK WHICH
			!LOOKS LIKE A TELLIST ENTRY (FOR NAMESEARCH)
MAP STRNAME P1;		!SO CAN USE IT TO INITIALIZE MYPPN

ROUTINE DOGETTABS=
BEGIN
REGISTER T;
GETDATE(T);
DAY _ (.T MOD 31) + 1;  MONTH _ (.T/31) MOD 12; YEAR _ ((.T/31)/12) + 1964;
GETMSTIME(T);
TIME _ .T / 60000;
!DEFPROT _ GETTAB(#16,#12)^(-27);	!DEFAULT FILE PROTECTION CODE
DEFPROT _ GETTAB(#140,-1);		![ERCC] DEFAULT TO JOB SET PROTECTION
MYNAME[0] _ GETTAB(#31,-1);	!MY NAME (FIRST HALF) IN SIXBIT
MYNAME[1] _ GETTAB(#32,-1);	!MY NAME (SECOND HALF)
MYPPN _ GETTAB(#2,-1);		!MY PPN (BOTH HALVES...)
P1_MYPPN<ADDR>;			!EDIT 215
P1[NM2]_P1[NAMEFLAGS]_0; P1[PPNFLAG]_1;
MYJOB _ CALLI (P1,#30); !MY JOB NUMBER
NOW _ GETTAB(#11,#53);		!NOW IN UNIVERSAL FORMAT
WEEKDAY _ (.NOW<LH>+3) MOD 7;	!DAY OF WEEK, SUNDAY = 0
IAMBATCH _ (GETTAB(#40,-1)^(-25)) AND 1;	
IF .MYPPN EQL #1000002
    THEN IAMGOD_1
    ELSE IAMGOD_(IF CSLHACK THEN (GETTAB(6,-1)^(-6)) AND 1 ELSE 0);
LOGGEDIN _ (T_.MYJOB*-1; IFSKIP CALLI(T,#61) %JOBSTS % THEN .T<34,1> ELSE 0);
!**ERCC** IF .LOGGEDIN THEN ELSE BATCH _ 1;	!NO USER QUESTIONS IF NOT LOGGEDIN
END;

!
!ROUTINE TO SCAN SWITCH.INI FOR SWITCHES THAT THE USER WISHES TO HAVE SET
!BY DEFAULT.
!
ROUTINE SWITCHINISCAN=
BEGIN REGISTER STRCHAR C;

!FIRST SET UP THE DEFAULT VALUES FOR ALL SWITCHES.

CCPRT_1;			!PRINT "CC:LIST" IF MANY RECEIVERS
QUERYAFTER_1;			!QUERY USER AFTER MESSAGE TYPED
DISPOPTION_2;			!SAVE MESSAGES (SEE DISPOSETABLE IN TELMES)
IAMSMART_0;			!I AM DUMB


!SET UP INITIAL GROUPLIST  TO BE EMPTY
IF NOT YORKHACK THEN INITSTORAGE(GROUPLIST,3+HEADERSIZE,(3+HEADERSIZE)*10);


!NOW SCAN SWITCH.INI
ZEROSPEC();
FILESPEC[FILEDEVICE]_SIXBIT 'DSK'; FILESPEC[FILENAME]_SIXBIT 'SWITCH'; FILESPEC[EXTENSION]_SIXBIT "INI";
FILESPEC[FILESTATUS]_0; FILESPEC[FILEBUFS]_DSKBUFH<ADDR>; FILESPEC[SFDPTR]_.MYPPN;


DSKOPEN_1; SWITCHSCAN_1;		!READING FROM A DISK FILE, (SWITCH.INI)
IF OPENSPEC(1)
    THEN
	DO
	    BEGIN
	    IF SIXREAD() EQL SIXBIT 'TELL'
		THEN
		   DO
		     BEGIN
		     DO C_CHARBSU() UNTIL .C[CLASS] NEQ BLANK;
		     IF .C[CLASS] EQL SLASH
			THEN (SWITCHRTN(); C_CHARBSU())
			ELSE
			    IF .C[CLASS] NEQ EOL AND .C[CLASS] NEQ EOF
				THEN
				    (TYPE ('Invalid character "'); TYPECHR(.C[ASCII]);
 				    TYPE ('" encountered while scanning SWITCH.INI?M?J'));
		     END
		  UNTIL
		     .C[CLASS] EQL EOL OR .C[CLASS] EQL EOF
		ELSE
		  DO C_CHARBSU() UNTIL .C[CLASS] EQL EOL OR .C[CLASS] EQL EOF;
	    END
	UNTIL
	    .C[CLASS] EQL EOF;
DSKOPEN_0;				!NO LONGER READING FROM DISK FILE
SWITCHSCAN_0;				!NO LONGER READING SWITCH.INI
IF .IAMBATCH				!BATCH JOBS CAN'T ANSWER QUESTIONS
    THEN (DISPOPTION_2; QUERYBEFORE_QUERYAFTER_0);
END;
!
!ROUTINE TO SCAN SYS:TELL.INI FOR SWITCHES THAT THE SYSTEM MANAGER WISHES TO HAVE SET
!BY DEFAULT FOR EVERYONE.
!
ROUTINE TELLINISCAN=
IF YORKHACK THEN BEGIN REGISTER STRCHAR C;

!FIRST SET UP THE DEFAULT VALUES FOR ALL SWITCHES.

CCPRT_1;			!PRINT "CC:LIST" IF MANY RECEIVERS
QUERYAFTER_1;			!QUERY USER AFTER MESSAGE TYPED
DISPOPTION_2;			!SAVE MESSAGES (SEE DISPOSETABLE IN TELMES)
IAMSMART_0;			!I AM DUMB


!SET UP INITIAL GROUPLIST  TO BE EMPTY
INITSTORAGE(GROUPLIST,3+HEADERSIZE,(3+HEADERSIZE)*10);


!NOW SCAN SYS:TELL.INI
ZEROSPEC();
FILESPEC[FILEDEVICE]_SIXBIT 'SYS'; FILESPEC[FILENAME]_SIXBIT 'TELL'; FILESPEC[EXTENSION]_SIXBIT "INI";
FILESPEC[FILESTATUS]_0; FILESPEC[FILEBUFS]_DSKBUFH<ADDR>; FILESPEC[SFDPTR]_0;


DSKOPEN_1; SWITCHSCAN_1;		!READING FROM A DISK FILE, (SYS:TELL.INI)
IF OPENSPEC(1)
    THEN
	DO
	    BEGIN
		     DO C_CHARBSU() UNTIL .C[CLASS] NEQ BLANK;
		     IF .C[CLASS] EQL SLASH
			THEN (SWITCHRTN(); C_CHARBSU())
			ELSE
			    IF .C[CLASS] NEQ EOL AND .C[CLASS] NEQ EOF
				THEN
				    (TYPE ('Invalid character "'); TYPECHR(.C[ASCII]);
 				    TYPE ('" encountered while scanning SYS:TELL.INI?M?J'));
	    END
	UNTIL
	    .C[CLASS] EQL EOF;
DSKOPEN_0;				!NO LONGER READING FROM DISK FILE
SWITCHSCAN_0;				!NO LONGER READING SWITCH.INI
IF .IAMBATCH				!BATCH JOBS CAN'T ANSWER QUESTIONS
    THEN (DISPOPTION_2; QUERYBEFORE_QUERYAFTER_0);
END;
!
!THIS IS THE TOP LEVEL OF THE TELL PROGRAM.  IT DOES VARIOUS INITIALIZATION-
!TYPE THINGS, THEN SCANS THE COMMAND (RESCANLINE FOR THE FIRST TIME) AND DISPATCHES
!TO THE APPROPRIATE COMMAND HANDLER.
!
SIMULATEMESSAGE_.VREG;	!USE CCL SETTING TO DETERMINE IF ACT LIKE MESSAG
RESET;				!RESET THE WORLD
FLAGWORD_0;

CHECKBUILD();			!IF NAMETABLE NOT BUILT, GO BUILD IT
MACINI();				!GO INITIALIZE MACRO THINGS
DOGETTABS();				!GET VARIOUS JOB PARAMETERS
IF YORKHACK THEN TELLINISCAN();		!SET SYSTEM MANAGERS DEFULTS FROM SYS:TELL.INI
IF .LOGGEDIN THEN SWITCHINISCAN();	!GO SCAN SWITCH.INI AND SET UP GLOBAL DEFAULTS

IFSKIP RESCANLINE THEN IF .SIMULATEMESSAGE THEN RESCAN(#12) ELSE TYPECHR("*");

!THIS IS THE MAIN LOOP. WE WAIT FOR A COMMAND, THEN DECIPHER IT AND DISPATCH TO
!THE STATEMENT HANDLER.
REPEAT
    BEGIN OWN TEMP;
    FLAGWORD<LH>_0;		!CLEAR TEMPORARY FLAGS
    SAVEFLAGS_.FLAGWORD<RH>;	!SAVE FLAGS FROM SWITCH.INI TO RESTORE ON NEW COMMAND
    SAVEJOBFF_@JOBFF;		!SAVE FIRST FREE LOC IN LOW SEGMENT AFTER GROUP TABLES
    TEMP _IF .SIMULATEMESSAGE
	    THEN SIXBIT 'MESSAG'		!MESSAGE COMMAND
	    ELSE SIXREAD();
    IF .TEMP EQL 0
	THEN CLREOL()
	ELSE CASE MATCHNAME(.TEMP,COMTAB)+2
	OF SET
	(TYPE ('??Ambiguous command, HELP for help?M?J');CLREOL());
	(TYPE ('??Unknown command, HELP for help?M?J');CLREOL());
	(RUNFLAG_1; CLREOL());  !0 -- START COMMAND, REMEMBER HOW WE ENTERED
	(RUNFLAG_1; CLREOL());  !1 -- RUN COMMAND, REMEMBER IT
	TELLCOMMAND(0);         !2 -- THE TELL COMMAND
	MESSAGECOMMAND();       !3 -- PRINT MESSAGES
	(TYPEADR(TELLHELP); CLREOL());	!4 -- HE IS CONFUSED
	WHOCOMMAND();		!5 -- THE WHO COMMAND
	(IF .IAMGOD THEN ALIASCOMMAND(); CLREOL());	!6 -- THE ALIAS COMMAND
	TELLCOMMAND(0);         !7 -- THE MAIL COMMAND
	TES;
    FLAGWORD<RH>_.SAVEFLAGS;	!RESTORE FLAGS FROM SWITCH.INI WHICH MAY HAVE CHANGED
    JOBFF<0,36>_.SAVEJOBFF;	!RESTORE FIRST FREE LOC IN LOW SEG TO RECLAIM SPACE
    IF NOT .RUNFLAG  THEN STOP();
    TYPECHR("*");
    END;
END;

