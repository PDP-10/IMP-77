MODULE TELWHO(RESERVE(4,5,6))=
BEGIN

REQUIRE	DSK:TELPRM.BLI;

EXTERNAL    TELLPARSE,	!ROUTINE TO PARSE A LIST OF USERS
	    MYPPN,	!MY PPN
	    PRTNM,	!ROUTINE TO PRINT A NAME
	    TTYTYPE,	!ROUTINE TO TYPE A CHAR ON THE TTY
	    PPNPRINT,	!ROUTINE TO PRINT A PPN
	    NEXTITEM,	!ROUTINE TO RETURN NEXT ITEM OF A LIST
	    TYPNMB,	!ROUTINE TO TYPE A NAME BLOCK IN A FANCY WAY
	    PPNSEARCH,	!ROUTINE TO SEARCH FOR PPNS
	    NAMESEARCH,	!ROUTINE TO SEARCH FOR NAMES
	    INITSCAN,	!ROUTINE TO INITIALIZE A LIST FOR SCANNING
	    PPNLIST,	!LIST HEADER FOR PPN'S
	    EXPECTING,	!FLAG SET IF MORE ON NEXT LINE
	    CLREOL,	!ROUTINE TO CLEAR TO END OF LINE
	    RESCAN,	!ROUTINE TO RESCAN A CHARACTER
	    FLAGWORD,	!FLAGS
	    TELLIST;	!PEOPLE TO TELL IN UNEXPANDED FORM

	MAP STRCOREBLK TELLIST:PPNLIST;

	OWN T,GETIND,SAVET,PNTR;

	MAP STRNAME T:PNTR:SAVET;

GLOBAL ROUTINE WHOCOMMAND=
BEGIN	LABEL GETUSERS,WHOLOOP,WHLOOP;
	INITSTORAGE(TELLIST,3,30);	!SET TO STORE NAMES
	INWHO_INDIRECTOK_1;	!WE ARE IN WHO. INDIRECT IS ALL RIGHT.
GETUSERS:
	REPEAT
	    BEGIN
	    EXPECTING_0;	!FLAG SET IF WE CAN EXPECT MORE USERS
	    TELLPARSE(1);	!GET A LINE. PROMPT IF NEEDED
	    CLREOL();		!CLEAR REST OF LINE
	    IF NOT .EXPECTING AND .TELLIST[ITEMCOUNT] GTR 0
		THEN LEAVE GETUSERS;
	    RESCAN(#12);	!RESCAN A LF SO TELLPARSE WILL PROMPT
	    END;
	INITSCAN(TELLIST);	!PREPARE TO SCAN USER LIST
	GETIND_1;		!YES, WE SHOULD GET A NEW ITEM
WHOLOOP:REPEAT
	    BEGIN
	    INITSTORAGE(PPNLIST,1,10);	!PREPARE TO GET A LIST OF PPN'S
	    IF .GETIND THEN T_NEXTITEM(TELLIST);
	    GETIND_1; SAVET_.T;	!GET A NEW ITEM NEXT TIME. SAVE FIRST ITEM
	    IF .T EQL 0 THEN LEAVE WHOLOOP;	!DONE
	    IF .T[PPNFLAG]
		THEN IF .MYPPN<LH> NEQ 1
		    THEN IF .T[NM2]<LH> EQL 0 OR .T[NM2]<RH> EQL 0
			THEN PPNSEARCH(.T,2)
			ELSE
		    ELSE PPNSEARCH(.T,2)
		ELSE NAMESEARCH(.T,2);
WHLOOP:	    REPEAT
		BEGIN
		T_NEXTITEM(TELLIST);	!GET THE FOLLOWING ITEM
		IF .T EQL 0 THEN (GETIND_0; LEAVE WHLOOP);
		IF NOT .T[DELFLAG] THEN (GETIND_0; LEAVE WHLOOP);
		(IF .T[PPNFLAG] THEN PPNSEARCH ELSE NAMESEARCH)(.T,2);
		END;
	    IF .PPNLIST[ITEMCOUNT] GTR 0
	      THEN
		BEGIN
		IF .TELLIST[ITEMCOUNT] GTR 1
		  THEN
		    BEGIN
		    IF NOT .SAVET[PPNFLAG] AND .ASKQUEST
		      THEN
			SAVET_@.PPNLIST[HEADER];
		    TYPNMB(.SAVET);
		    TYPECHR("=");	!IDENTIFY
		    END;
		INITSCAN(PPNLIST);		!PREPARE TO SCAN USER LIST
		P3_TTYTYPE<ADDR>;		!OUTPUT TO TTY
		DECR I FROM .PPNLIST[ITEMCOUNT] TO 1
		  DO
		    BEGIN
		    DO
			PNTR_NEXTITEM(PPNLIST)
		    UNTIL
			(PNTR_@.PNTR) NEQ 0;
		    IF .SAVET[PPNFLAG]
			THEN PRINTNAME(.PNTR)
			ELSE PPNPRINT(.PNTR[PPNOFNAME]);
		    IF .I GTR 1 THEN TYPECHR(",");
		    END;
		TYPE('?M?J');
		END;
	    END;
	RETURN 0;
END;
END;
